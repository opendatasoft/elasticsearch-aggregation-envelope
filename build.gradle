buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${es_version}"
    }
}

// the spotless plugin is dedicated to format the code
plugins {
  id("com.diffplug.spotless") version "7.0.3"
}

repositories {
    mavenLocal()
    mavenCentral()
}

// add -Xlint:deprecation to check the Elasticsearch deprecation warning at compile-time
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}


group = 'org.elasticsearch.plugin'
version = "${plugin_version}"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'


// automatic formatting configuration (the same configuration as elasticsearch)
spotless {
    java {
        importOrderFile('config/elastic.importorder') // import order file as exported from elastic
        eclipse().configFile('config/formatterConfig.xml')
        trimTrailingWhitespace()
        target 'src/**/*.java'
    }
}

check.dependsOn spotlessCheck

esplugin {
    name = 'envelope-aggregation'
    description = 'Returns envelope of geo points'
    classname = 'org.opendatasoft.elasticsearch.plugin.EnvelopeAggregation'
    licenseFile = rootProject.file('LICENSE')
    noticeFile = rootProject.file('README.md')
}

dependencies {
    implementation "org.elasticsearch:elasticsearch:${es_version}"

    implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.19.0'
    implementation group: 'org.locationtech.jts.io', name: 'jts-io-common', version: '1.19.0'
    implementation group: 'org.locationtech.spatial4j', name: 'spatial4j', version: '0.8'

    yamlRestTestImplementation "org.elasticsearch.test:framework:${es_version}"
    yamlRestTestImplementation "org.elasticsearch.test:yaml-rest-runner:${es_version}"
    yamlRestTestImplementation "org.apache.logging.log4j:log4j-core:2.17.1"
}

// Make sure the ES distribution used for rest tests is the "complete" variant
// Otherwise weirds errors occur (like the geo_shape type is not handled)
testClusters.configureEach {
    testDistribution = 'DEFAULT'
    // disable security to disable failing warnings
    setting 'xpack.security.enabled', 'false'
}

test.dependsOn yamlRestTest
